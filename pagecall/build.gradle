plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

ext {
    GITHUB_REPO = "pagecall-android-sdk"
    GITHUB_ORG = "pagecall"
    PUBLISH_GROUP_ID = "com.pagecall"
    PUBLISH_VERSION = "0.0.3"

    // secret key 들은 "afterEvaluate" 과정에서 "local.properties" 파일에서 가져옵니다.
    signing {
        keyId = ""
        password = ""
        secretKeyRingFile = ""
    }
    ossrhUsername = ""
    ossrhPassword = ""
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

android {
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        versionCode 0
        versionName = PUBLISH_VERSION // Update `version` field of PagecallWebView as you change this
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    api 'com.google.code.gson:gson:2.8.9'
    api 'io.github.haiyangwu:mediasoup-client:3.4.0'
}

afterEvaluate {
    File secretPropsFile = project.rootProject.file("local.properties")
    if (secretPropsFile.exists()){
        Properties p = new Properties()
        new FileInputStream(secretPropsFile).withCloseable { fis -> p.load(fis)}
        p.each { key, value -> ext[key.toString()] = value }
    }

    publishing {
        repositories {
            maven {
                name 'sonatype'
                url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'

                // nexus manager account
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }

        publications {
            release(MavenPublication) {
                groupId 'com.pagecall'
                artifactId 'pagecall-android'
                version = PUBLISH_VERSION
                artifact("$buildDir/outputs/aar/pagecall-release.aar")

                pom {
                    name = GITHUB_REPO
                    description = 'lightweight Android SDK for embedding Pagecall webapp with seamless audio support.'
                    url = "https://github.com/$GITHUB_ORG/$GITHUB_REPO"
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = GITHUB_ORG
                            name = GITHUB_ORG
                            email = 'support@pagecall.com'
                        }
                    }
                    scm {
                        url = "https://github.com/$GITHUB_ORG/${GITHUB_REPO}.git"
                    }
                }
            }
        }
    }
}


signing {
    sign publishing.publications
}